plugins {
    id 'java'
    id 'ru.vyarus.quality' version '4.2.2'
    id "io.freefair.lombok" version "5.0.0-rc6"
    id 'java-library'
    id 'maven-publish'
    id "de.undercouch.download" version "4.1.1"
}

quality {
    checkstyleVersion = '8.32'
    checkstyle = true
    spotbugs = false
    pmd = false
    configDir = 'config'
}

repositories {
    jcenter()
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

group 'com.github.sgov'
version = '1.5.4'

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.topbraid:shacl:1.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.4.6'
}

jar {
    archiveFileName = 'sgov-validator-' + new Date().format('yyyyMMddHHmmss') + '.jar'
}

ext.genOutputDir = file("$buildDir/generated-resources")

task generateZSGoVUFOMapping(type: Download) {
    src 'https://raw.githubusercontent.com/opendata-mvcr/ssp/master/content/z-sgov/z-sgov-mapov%C3%A1n%C3%AD-ufo.ttl'
    dest new File("$genOutputDir", 'z-sgov-mapping.ttl')
}

sourceSets.main.output.dir ext.genOutputDir, builtBy: generateZSGoVUFOMapping

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/opendata-mvcr/sgov-validator")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
        maven {
            name = "KbssMavenRepo"
            url = "sftp://kbss.felk.cvut.cz:22/var/www/m2repo"
            credentials {
                username = project.findProperty("kbss.user") ?: System.getenv("KBSS_USER")
                password = project.findProperty("kbss.password") ?: System.getenv("KBSS_PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}